plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.10'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'cn.projectan'
version = '1.0.0'
description = 'Strix'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

jar {
    // 用于子项目引入的Jar文件
    // Maven导入语句：
    // mvn install:install-file -DgroupId=cn.projectan -DartifactId=Strix -Dversion=1.0.0 -Dpackaging=jar -Dfile=build/libs/Strix-plain.jar
    archiveFileName = "Strix-plain.jar"
    exclude('application.yml')
    exclude('application-*.yml')
    from {
        configurations.runtimeClasspath.filter { it.name.endsWith('strix-captcha-1.1.1.jar') }.collect {
            zipTree(it).matching {
                exclude 'META-INF/services/cn.projectan.captcha.service.CaptchaCacheService'
            }
        }
    }
}

bootJar {
    // 可直接运行的Jar文件
    archiveFileName = "Strix.jar"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // SpringBoot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // mysql
    runtimeOnly 'com.mysql:mysql-connector-j:8.0.32'
    implementation 'com.baomidou:mybatis-plus-boot-starter:3.5.3.1'
    implementation 'com.baomidou:mybatis-plus-generator:3.5.3.1'
    implementation 'org.apache.velocity:velocity-engine-core:2.3'
    implementation 'p6spy:p6spy:3.9.1'
    // strix-module
    implementation files('libs/strix-captcha-1.1.1.jar')
    // other
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.aliyun:aliyun-java-sdk-core:4.6.0'
    implementation 'com.aliyun.oss:aliyun-sdk-oss:3.16.2'
    implementation 'cn.hutool:hutool-all:5.8.11'
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.71'
    implementation 'com.github.javen205:IJPay-WxPay:2.9.7'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.github.oshi:oshi-core:6.2.2'
    implementation 'org.lionsoul:ip2region:2.6.5'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}
