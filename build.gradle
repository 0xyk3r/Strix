plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'cn.projectan'
version = '1.5.20'
description = 'Strix'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

jar {
    // 用于子项目引入的Jar文件
    exclude('application.yml')
    exclude('application-*.yml')
    from {
        configurations.runtimeClasspath.filter { it.name.endsWith('strix-captcha-1.2.0.jar') }.collect {
            zipTree(it).matching {
                exclude 'META-INF/services/cn.projectan.captcha.service.CaptchaCacheService'
                exclude 'META-INF/spring-configuration-metadata.json'
            }
        }
    }
}

bootJar {
    // 可直接运行的Jar文件
    archiveFileName = "Strix.jar"
}

test {
    useJUnitPlatform()
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    // Database - MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'com.baomidou:mybatis-plus-boot-starter:3.5.3.2'
    implementation 'com.baomidou:mybatis-plus-generator:3.5.3.2'
    implementation 'com.baomidou:dynamic-datasource-spring-boot3-starter:4.1.3'
    implementation 'org.apache.velocity:velocity-engine-core:2.3'
    // Database - ClickHouse
    implementation 'com.clickhouse:clickhouse-jdbc:0.5.0'
    runtimeOnly 'org.lz4:lz4-java:1.8.0'
    // Strix Module
    implementation files('libs/strix-captcha-1.2.0.jar')
    // Quartz
    implementation('org.quartz-scheduler:quartz') {
        exclude group: 'com.mchange', module: 'c3p0'
    }
    // Other Tools
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'cn.hutool:hutool-all:5.8.22'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.76'
    implementation 'org.lionsoul:ip2region:2.7.0'
    implementation 'org.apache.tika:tika-core:2.9.0'
    implementation 'com.github.oshi:oshi-core:6.4.6'
    implementation 'org.reflections:reflections:0.10.2'
    // Third Party
    implementation 'com.aliyun:aliyun-java-sdk-dysmsapi:2.2.1'
    implementation 'com.aliyun.oss:aliyun-sdk-oss:3.17.1'
    implementation 'com.github.javen205:IJPay-WxPay:2.9.9'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}
