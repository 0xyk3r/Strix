plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.graalvm.buildtools.native' version '0.10.1'
}

group = 'cn.projectan'
version = '2.2.3'
description = 'Strix'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

jar {
    // 用于子项目引入的Jar文件
    // FIXME 这里会影响 Native 构建
//    exclude('application.yml')
//    exclude('application-*.yml')
//    from {
//        configurations.runtimeClasspath.filter { it.name.endsWith('strix-captcha-1.3.1.jar') }.collect {
//            zipTree(it).matching {
//                exclude 'META-INF/services/cn.projectan.captcha.service.CaptchaCacheService'
//                exclude 'META-INF/spring-configuration-metadata.json'
//            }
//        }
//    }
}

bootJar {
    // 可直接运行的Jar文件
    archiveFileName = "Strix.jar"
}

// 屏蔽部分依赖传递
configurations.all {
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // Spring Boot - Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.redisson:redisson-spring-boot-starter:3.29.0'
    // Database - MySQL
    implementation 'p6spy:p6spy:3.9.1'
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'com.baomidou:mybatis-plus-spring-boot3-starter:3.5.6'
    implementation 'com.baomidou:mybatis-plus-generator:3.5.6'
    implementation 'com.baomidou:dynamic-datasource-spring-boot3-starter:4.3.0'
    implementation 'org.apache.velocity:velocity-engine-core:2.3'
    // Database - ClickHouse
    implementation 'com.clickhouse:clickhouse-jdbc:0.6.0-patch3'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
    implementation 'org.lz4:lz4-java:1.8.0'
    // Quartz
    implementation('org.quartz-scheduler:quartz') {
        exclude group: 'com.mchange', module: 'c3p0'
    }
    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    // Other Tools
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'cn.hutool:hutool-core:5.8.27'
    implementation 'cn.hutool:hutool-crypto:5.8.27'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'org.lionsoul:ip2region:2.7.0'
    // Third Party
    implementation 'com.aliyun:aliyun-java-sdk-dysmsapi:2.2.1'
    implementation 'com.aliyun.oss:aliyun-sdk-oss:3.17.4'
    implementation 'com.github.javen205:IJPay-WxPay:2.9.10-17'
    implementation 'com.alipay.sdk:alipay-sdk-java:4.39.58.ALL'
    implementation('com.github.javen205:IJPay-AliPay:2.9.10-17') {
        exclude group: 'com.alipay.sdk', module: 'alipay-sdk-java'
    }

    // GraalVM
    compileOnly 'org.graalvm.sdk:graal-sdk:24.0.1'
}

test {
    useJUnitPlatform()
}

tasks.named('bootBuildImage') {
    imageName = 'projectan.cn/strix:latest'
    builder = "paketobuildpacks/builder-jammy-base:latest"
    environment = [
            'BP_NATIVE_IMAGE'                : 'true',
            'BP_NATIVE_IMAGE_BUILD_ARGUMENTS': '-H:+AddAllCharsets --enable-url-protocols=http --enable-url-protocols=https --features=cn.projectan.strix.aot.LambdaRegistrationFeature --features=cn.projectan.strix.aot.BouncyCastleFeature --initialize-at-run-time=sun.net.dns.ResolverConfigurationImpl --rerun-class-initialization-at-runtime=org.bouncycastle.crypto.prng.SP800SecureRandom --rerun-class-initialization-at-runtime=org.bouncycastle.jcajce.provider.drbg.DRBG$Default --rerun-class-initialization-at-runtime=org.bouncycastle.jcajce.provider.drbg.DRBG$NonceAndIV -H:IncludeResources=".*/?.*properties$" -H:IncludeResources=".*/?.*xml$" -H:IncludeResources=".*/?.*json$" -H:IncludeResources=".*/?.*xdb$" -H:IncludeResources=".*/?.*types$" -H:IncludeResources=".*/?.*png$" -H:IncludeResources=".*/?.*jpg$" -H:IncludeResources=".*/?.*jpeg$"',
    ]
    buildpacks = ["gcr.io/paketo-buildpacks/java-native-image:latest"]
    docker {
        host = 'tcp://192.168.31.188:2375'
        tlsVerify = false
    }
}

graalvmNative {
    binaries {
        main {
            buildArgs.add('--verbose')
            buildArgs.add('--no-fallback')
            buildArgs.add('-H:+UnlockExperimentalVMOptions')
            buildArgs.add('-H:+AddAllCharsets')
            buildArgs.add('--enable-url-protocols=http')
            buildArgs.add('--enable-url-protocols=https')
            buildArgs.add('--features=cn.projectan.strix.aot.LambdaRegistrationFeature')
            buildArgs.add('--features=cn.projectan.strix.aot.BouncyCastleFeature')
            buildArgs.add('--initialize-at-run-time=sun.net.dns.ResolverConfigurationImpl')
            buildArgs.add('--rerun-class-initialization-at-runtime=org.bouncycastle.crypto.prng.SP800SecureRandom')
            buildArgs.add('--rerun-class-initialization-at-runtime=org.bouncycastle.jcajce.provider.drbg.DRBG$Default')
            buildArgs.add('--rerun-class-initialization-at-runtime=org.bouncycastle.jcajce.provider.drbg.DRBG$NonceAndIV')
            buildArgs.add('-H:IncludeResources=".*/?.*properties$"')
            buildArgs.add('-H:IncludeResources=".*/?.*xml$"')
            buildArgs.add('-H:IncludeResources=".*/?.*json$"')
            buildArgs.add('-H:IncludeResources=".*/?.*xdb$"')
            buildArgs.add('-H:IncludeResources=".*/?.*types$"')
            buildArgs.add('-H:IncludeResources=".*/?.*png$"')
            buildArgs.add('-H:IncludeResources=".*/?.*jpg$"')
            buildArgs.add('-H:IncludeResources=".*/?.*jpeg$"')
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}
