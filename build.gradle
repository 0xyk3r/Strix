plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'cn.projectan'
version = '1.6.5.1'
description = 'Strix'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

jar {
    // 用于子项目引入的Jar文件
    exclude('application.yml')
    exclude('application-*.yml')
    from {
        configurations.runtimeClasspath.filter { it.name.endsWith('strix-captcha-1.2.0.jar') }.collect {
            zipTree(it).matching {
                exclude 'META-INF/services/cn.projectan.captcha.service.CaptchaCacheService'
                exclude 'META-INF/spring-configuration-metadata.json'
            }
        }
    }
}

bootJar {
    // 可直接运行的Jar文件
    archiveFileName = "Strix.jar"
}

tasks.named('test') {
    useJUnitPlatform()
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    // Spring Boot - Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    // Database - MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'com.baomidou:mybatis-plus-spring-boot3-starter:3.5.5'
    implementation 'com.baomidou:mybatis-plus-generator:3.5.5'
    implementation 'com.baomidou:dynamic-datasource-spring-boot3-starter:4.3.0'
    implementation 'org.apache.velocity:velocity-engine-core:2.3'
    // Database - ClickHouse
    implementation 'com.clickhouse:clickhouse-jdbc:0.6.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
    runtimeOnly 'org.lz4:lz4-java:1.8.0'
    // Strix Module
    implementation files('libs/strix-captcha-1.2.0.jar')
    // Quartz
    implementation('org.quartz-scheduler:quartz') {
        exclude group: 'com.mchange', module: 'c3p0'
    }
    // Other Tools
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'cn.hutool:hutool-all:5.8.26'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.77'
    implementation 'org.lionsoul:ip2region:2.7.0'
    implementation 'org.apache.tika:tika-core:2.9.1'
    implementation 'com.github.oshi:oshi-core:6.4.13'
    implementation 'org.reflections:reflections:0.10.2'
    // Third Party
    implementation 'com.aliyun:aliyun-java-sdk-dysmsapi:2.2.1'
    implementation 'com.aliyun.oss:aliyun-sdk-oss:3.17.4'
    implementation 'com.github.javen205:IJPay-WxPay:2.9.10-17'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}
